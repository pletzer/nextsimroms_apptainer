module tovtk_mod

    implicit none

    contains

    subroutine zero_fill(num, n, str)
        ! Generate a n-sized string from an integer, prepending the string with 0s
        ! @param num: the integer to convert to a string
        ! @param n: the size of the string
        ! @param str: the output string
        integer, intent(in) :: num
        integer, intent(in) :: n
        character(len=*), intent(out) :: str

        character(len=20) :: fmt, n_str

        ! Dynamically create the format
        write(n_str, *) n
        fmt = '(I0.' // trim(n_str) // ')'
        ! Convert the integer to a zero-padded string
         write(str, fmt) num
            
    end subroutine zero_fill
    
    subroutine vtk_write_data(lons, lats, data, var_name, filename)

        real(8), intent(in) :: lons(:, :), lats(:, :), data(:, :)
        character(len=*), intent(in) :: var_name, filename

        integer :: iu, i, j, numPoints, numCells, ndx, ndy, nx, ny

        nx = size(lons, 1)
        ny = size(lons, 2)
        ndx = size(data, 1)
        ndy = size(data, 2)

        numPoints = nx * ny
        numCells = (nx - 1) * (ny - 1)

        open(newunit=iu, file=filename)
        write(iu, '(A)') '# vtk DataFile Version 3.0'
        write(iu, '(A)') 'File generated by tovtk module'
        write(iu, '(A)') 'ASCII'
        write(iu, '(A)') 'DATASET STRUCTURED_GRID'
        write(iu, '(A, I6, I6, I6)') 'DIMENSIONS ', size(lons, 1), size(lons, 2), 1
        write(iu, '(A, I6, A)') 'POINTS ', numPoints, ' double'
        do j = 1, size(lons, 2)
            do i = 1, size(lons, 1)
            write(iu, '(2E20.8, A)') &
                 lons(i, j), lats(i, j), ' 0.E0'
            enddo
        enddo

        if (nx == ndx .and. ny == ndy) then
            write(iu, '(A, I6)') 'POINT_DATA', numPoints
            !write(iu, '(A, A, A)') 'SCALARS ', trim(var_Name), ' double 1'
            ! VTK has issues with some field names
            write(iu, '(A, A, A)') 'SCALARS ', 'scalar', ' double 1'
            write(iu, '(A)') 'LOOKUP_TABLE default'
            do j = 1, ny
                do i = 1, nx
                    write(iu, '(E20.8)') data(i, j)
                enddo
            enddo
        else if (nx == ndx + 1 .and. ny == ndy + 1) then
            write(iu, '(A, I6)') 'CELL_DATA', numCells
            write(iu, '(A, A, A)') 'SCALARS ', trim(var_Name), ' double 1'
            write(iu, '(A)') 'LOOKUP_TABLE default'
            do j = 1, ndy
                do i = 1, ndx
                    write(iu, '(E20.8)') data(i, j)
                enddo
            enddo
        else
            print *, 'ERROR: tovtk.write_data: mismatch in array sizes. nx = ', nx, ' ndx = ', ndx, ' ny = ', ny, ' ndy = ', ndy
        endif
 
        close(unit=iu)

    end subroutine vtk_write_data

end module tovtk_mod