module tovtk_mod

    implicit none

    contains

    subroutine zero_fill(num, n, str)
        ! Generate a n-sized string from an integer, prepending the string with 0s
        ! @param num: the integer to convert to a string
        ! @param n: the size of the string
        ! @param str: the output string
        integer, intent(in) :: num
        integer, intent(in) :: n
        character(len=*), intent(out) :: str

        character(len=20) :: fmt, n_str

        ! Dynamically create the format
        write(n_str, *) n
        fmt = '(I0.' // trim(n_str) // ')'
        ! Convert the integer to a zero-padded string
         write(str, fmt) num
            
    end subroutine zero_fill
    
    subroutine vtk_write_data(values, var_name, filename)

        real(8), intent(in) :: values(:, :, :)
        character(len=*), intent(in) :: var_name, filename

        integer :: iu, i, j, k, numPoints, numCells, ndx, ndy, ndz, nx, ny, nz

        ndx = size(values, 1)
        ndy = size(values, 2)
        ndz = size(values, 3)
        ! data are cell centred
        nx = ndx + 1
        ny = ndy + 1
        nz = ndz + 1

        numPoints = nx * ny * nz
        numCells = (nx - 1) * (ny - 1) * (nz - 1)

        open(newunit=iu, file=filename)
        write(iu, '(A)') '# vtk DataFile Version 3.0'
        write(iu, '(A)') 'File generated by tovtk module'
        write(iu, '(A)') 'ASCII'
        write(iu, '(A)') 'DATASET RECTILINEAR_GRID'
        write(iu, '(A, I6, I6, I6)') 'DIMENSIONS ', nx, ny, nz
        write(iu, '(A, I6, A)') 'X_COORDINATES ', nx, ' double'
        do i = 0, nx - 1
            write(iu, '(E20.8)') real(i, 8)
        enddo
        write(iu, '(A, I6, A)') 'Y_COORDINATES ', ny, ' double'
        do j = 0, ny - 1
            write(iu, '(E20.8)') real(j, 8)
        enddo
        write(iu, '(A, I6, A)') 'Z_COORDINATES ', nz, ' double'
        do k = 0, nz - 1
            write(iu, '(E20.8)') real(k, 8)
        enddo
        write(iu, '(A, I6)') 'CELL_DATA', numCells
        write(iu, '(A, A, A)') 'SCALARS ', trim(var_Name), ' double 1'
        write(iu, '(A)') 'LOOKUP_TABLE default'
        do k = 1, ndz
            do j = 1, ndy
                do i = 1, ndx
                    write(iu, '(E20.8)') values(i, j, k)
                enddo
            enddo
        enddo
 
        close(unit=iu)

    end subroutine vtk_write_data

end module tovtk_mod