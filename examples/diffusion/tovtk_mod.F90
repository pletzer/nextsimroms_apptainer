module tovtk_mod

    implicit none

    contains

    subroutine zero_fill(num, n, str)
        ! Generate a n-sized string from an integer, prepending the string with 0s
        ! @param num: the integer to convert to a string
        ! @param n: the size of the string
        ! @param str: the output string
        integer, intent(in) :: num
        integer, intent(in) :: n
        character(len=*), intent(out) :: str

        character(len=20) :: fmt, n_str

        ! Dynamically create the format
        write(n_str, *) n
        fmt = '(I0.' // trim(n_str) // ')'
        ! Convert the integer to a zero-padded string
         write(str, fmt) num
            
    end subroutine zero_fill
    
    subroutine vtk_write_data(xs, ys, zs, values, var_name, filename)

        real(8), intent(in) :: xs(:), ys(:), zs(:)
        real(8), intent(in) :: values(:, :, :)
        character(len=*), intent(in) :: var_name, filename

        integer :: iu, i, j, k, numPoints, nx1, ny1, nz1

        nx1 = size(values, 1)
        ny1 = size(values, 2)
        nz1 = size(values, 3)

        numPoints = nx1 * ny1 * nz1

        open(newunit=iu, file=filename)
        write(iu, '(A)') '# vtk DataFile Version 3.0'
        write(iu, '(A)') 'File generated by tovtk module'
        write(iu, '(A)') 'ASCII'
        write(iu, '(A)') 'DATASET RECTILINEAR_GRID'
        write(iu, '(A, I6, I6, I6)') 'DIMENSIONS ', nx1, ny1, nz1
        write(iu, '(A, I6, A)') 'X_COORDINATES ', nx1, ' double'
        do i = 1, nx1
            write(iu, '(E20.8)') xs(i)
        enddo
        write(iu, '(A, I6, A)') 'Y_COORDINATES ', ny1, ' double'
        do j = 1, ny1
            write(iu, '(E20.8)') ys(j)
        enddo
        write(iu, '(A, I6, A)') 'Z_COORDINATES ', nz1, ' double'
        do k = 1, nz1
            write(iu, '(E20.8)') zs(k)
        enddo
        write(iu, '(A, I6)') 'POINT_DATA', numPoints
        write(iu, '(A, A, A)') 'SCALARS ', trim(var_Name), ' double 1'
        write(iu, '(A)') 'LOOKUP_TABLE default'
        do k = 1, nz1
            do j = 1, ny1
                do i = 1, nx1
                    write(iu, '(E20.8)') values(i, j, k)
                enddo
            enddo
        enddo
 
        close(unit=iu)

    end subroutine vtk_write_data

end module tovtk_mod