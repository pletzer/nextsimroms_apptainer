module tovtk_mod

    implicit none

    contains

    function zero_fill(num, n) result(str)
        integer, intent(in) :: num ! the number of convert to a string
        integer, intent(in) :: n   ! the length of the string
        character(len=32) :: str

        character(len=20) :: fmt, n_str

        ! Dynamically create the format
        write(n_str, *) n
        fmt = '(I0.' // trim(n_str) // ')'
        ! Convert the integer to a zero-padded string
        write(str, fmt) num
            
    end function zero_fill

    subroutine vtk_write_data_xyz(lons_crnr, lats_crnr, data, var_name, filename)

        real(8), intent(in) :: lons_crnr(:, :, :), lats_crnr(:, :, :), data(:, :)
        character(len=*), intent(in) :: var_name, filename

        integer :: iu, i, j, k, numPoints, numCells, nx, ny
        real(8) :: x, y, z, rho
        real(8), parameter :: pi = 3.141592653589793116_8

        nx = size(lons_crnr, 1) ! number of longitude cells
        ny = size(lons_crnr, 2) ! number of latitude cells

        numCells = nx * ny
        numPoints = numCells * 4 ! each cell has 4 points, no connectivity between cells

        open(newunit=iu, file=filename)
        write(iu, '(A)') '# vtk DataFile Version 3.0'
        write(iu, '(A)') 'File generated by tovtk module'
        write(iu, '(A)') 'ASCII'
        write(iu, '(A)') 'DATASET UNSTRUCTURED_GRID'
        write(iu, '(A, I20, A)') 'POINTS', numPoints, ' double'
        do j = 1, ny
            do i = 1, nx
                do k = 1, 4
                    ! project onto sphere
                    z = sin(lats_crnr(i, j, k) * pi/180.)
                    rho = cos(lats_crnr(i, j, k) * pi/180.)
                    x = rho * cos(lons_crnr(i, j, k) * pi/180.)
                    y = rho * sin(lons_crnr(i, j, k) * pi/180.)
                    write(iu, '(3F20.8)') x, y, z
                enddo
            enddo
        enddo
        write(iu, '(A, I20, I20)') 'CELLS', numCells, numCells*(4 + 1)
        do j = 1, ny
            do i = 1, nx
                write(iu, '(I3, 4I20)') 4, &
                    4*((j-1)*nx + i-1) + 0, &
                    4*((j-1)*nx + i-1) + 1, &
                    4*((j-1)*nx + i-1) + 2, &
                    4*((j-1)*nx + i-1) + 3
            enddo
        enddo
        write(iu, '(A, I20)') 'CELL_TYPES', numCells      
        do j = 1, ny
            do i = 1, nx
                write(iu, '(I3)') 9 ! quad
            enddo
        enddo

        write(iu, '(A, I20)') 'CELL_DATA ', numCells
        write(iu, '(A, A, A, I20)') 'SCALARS ', var_name, ' double', 1
        write(iu, '(A)') 'LOOKUP_TABLE default'
        do j = 1, ny
            do i = 1, nx
                write(iu, '(F20.8)') data(i, j)
            enddo
        enddo

    end subroutine  vtk_write_data_xyz

    subroutine vtk_write_data_lonlat(lons_crnr, lats_crnr, data, var_name, filename)

        real(8), intent(in) :: lons_crnr(:, :, :), lats_crnr(:, :, :), data(:, :)
        character(len=*), intent(in) :: var_name, filename

        integer :: iu, i, j, k, numPoints, numCells, nx, ny

        nx = size(lons_crnr, 1) ! number of longitude cells
        ny = size(lons_crnr, 2) ! number of latitude cells

        numCells = nx * ny
        numPoints = numCells * 4 ! each cell has 4 points, no connectivity between cells

        open(newunit=iu, file=filename)
        write(iu, '(A)') '# vtk DataFile Version 3.0'
        write(iu, '(A)') 'File generated by tovtk module'
        write(iu, '(A)') 'ASCII'
        write(iu, '(A)') 'DATASET UNSTRUCTURED_GRID'
        write(iu, '(A, I20, A)') 'POINTS', numPoints, ' double'
        do j = 1, ny
            do i = 1, nx
                do k = 1, 4
                    write(iu, '(3F20.8)') lons_crnr(i, j, k), lats_crnr(i, j, k), 0._8
                enddo
            enddo
        enddo
        write(iu, '(A, I20, I20)') 'CELLS', numCells, numCells*(4 + 1)
        do j = 1, ny
            do i = 1, nx
                write(iu, '(I3, 4I20)') 4, &
                    4*((j-1)*nx + i-1) + 0, &
                    4*((j-1)*nx + i-1) + 1, &
                    4*((j-1)*nx + i-1) + 2, &
                    4*((j-1)*nx + i-1) + 3
            enddo
        enddo
        write(iu, '(A, I20)') 'CELL_TYPES', numCells      
        do j = 1, ny
            do i = 1, nx
                write(iu, '(I3)') 9 ! quad
            enddo
        enddo

        write(iu, '(A, I20)') 'CELL_DATA ', numCells
        write(iu, '(A, A, A, I20)') 'SCALARS ', var_name, ' double', 1
        write(iu, '(A)') 'LOOKUP_TABLE default'
        do j = 1, ny
            do i = 1, nx
                write(iu, '(F20.8)') data(i, j)
            enddo
        enddo
            
 
        close(unit=iu)

    end subroutine vtk_write_data_lonlat

end module tovtk_mod